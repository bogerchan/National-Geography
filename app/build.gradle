apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'bugly'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "geographic.boger.me.nationalgeographicdailyposter"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file('../store.jks')
            storePassword getValue('storePassword')
            keyAlias getValue('keyAlias')
            keyPassword getValue('keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-fresco.pro'
            signingConfig signingConfigs.release
        }
    }
    defaultConfig {
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
        resValue 'string', 'app_id_bugly', getValue('appIdBugly')
    }
}

def getValue(String key) {
    project.hasProperty(key) ? project.getProperty(key) : System.getenv(key)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.3.1'
    compile 'com.facebook.fresco:fresco:1.3.0'
    compile 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'jp.wasabeef:recyclerview-animators:2.2.6'
    compile 'com.lcodecorex:tkrefreshlayout:1.0.7'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.github.dinuscxj:LoadingDrawable:db4ceb6b90'
    compile 'com.zqc.opencc.android.lib:lib-opencc-android:0.8.0@aar'
    //bugly
    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
    compile 'com.tencent.bugly:nativecrashreport:latest.release'
    testCompile 'junit:junit:4.12'
}
repositories {
    mavenCentral()
}

bugly {
    appId = getValue('appIdBugly') // 注册时分配的App ID
    appKey = getValue('appKeyBugly') // 注册时分配的App Key
}